# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_vendor_min=symbiflow
_vendor=SymbiFlow
pkgname=$_vendor_min-vtr-git
_pkgname=vtr-verilog-to-routing
pkgver=8.0.0.rc2.r13657.8980e4621
pkgrel=1
pkgdesc="Verilog to Routing -- Open Source CAD Flow for FPGA Research ($_vendor fork)"
arch=('x86_64')
url="https://github.com/$_vendor/vtr-verilog-to-routing"
license=('MIT')
depends=('ctags' 'intel-tbb' 'capnproto')
makedepends=('git' 'cmake')
source=("$_vendor-$_pkgname::git+$url.git")
sha512sums=('SKIP')

pkgver() {
    cd $_vendor-$_pkgname

    printf '%s.r%s.%s' \
        $(git tag | sed -e '/^vtr/d' -e '/^vpr/d' -e 's|^v||' -e 's|-|.|' | tail -n1) \
        $(git rev-list --count HEAD) \
        $(git rev-parse --short HEAD)
}

build() {
  mkdir $_vendor-$_pkgname/build
  cd $_vendor-$_pkgname/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DEXTERNAL_CAPNP=True

  make
}

package() {
  cd $_vendor-$_pkgname/build

  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir"/usr/bin/*.a "$pkgdir"/usr/bin/test_* "$pkgdir"/usr/bin/*_test "$pkgdir"/usr/capnp

  for FILE in "$pkgdir"/usr/bin/*; do
    mv "$FILE" "$pkgdir/usr/bin/$_vendor_min-$(basename "$FILE")"
  done

  install -Dm 644 ../LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

