# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_prj=trellis
_pkgname=prj$_prj
pkgname=$_pkgname-git
pkgver=dummy
pkgrel=1
pkgdesc='Documentation and definitions for the Lattice ECP5 bit-stream format'
arch=('x86_64')
url='https://github.com/SymbiFlow/prjtrellis'
license=('custom:ISC' 'MIT')
depends=('boost-libs' "$_pkgname-db")
makedepends=('git' 'cmake' 'boost' 'python')
#             'python-sphinx' 'python-sphinx_rtd_theme' 'python-recommonmark')
optdepends=('python: Python support')
provides=({prj,lib}$_prj)
conflicts=({prj,lib}$_prj)
source=("git+$url.git")
sha512sums=('SKIP')

pkgver() {
    cd $_pkgname

    printf '%s.r%s.%s' \
        $(git tag | tail -n1 | sed 's|^v||') \
        $(git rev-list --count HEAD) \
        $(git rev-parse --short HEAD)
}

build() {
  mkdir $_pkgname/lib$_prj/build
  cd $_pkgname/lib$_prj/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release

  make

  # missing python-sphinxcontrib-markdown-symlinks
#  cd ../../docs
#
#  make html
}

package() {
  cd $_pkgname

  # Install documentation
#  install -dm 755 "$pkgdir"/usr/share/doc/$_pkgname
#  cp -r --no-preserve=ownership docs/_build/* "$pkgdir"/usr/share/doc/$_pkgname/
#  rm -rf "$pkgdir"/usr/share/doc/$_pkgname/html/.doctrees

  install -Dm 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd lib$_prj/build

  make DESTDIR="$pkgdir" install

  # The database is provided in a separate package
  rmdir "$pkgdir"/usr/share/$_prj/database
}

